buildscript {
    ext.kotlin_version = "1.8.10"
    ext.hilt_version = '2.44'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    repositories {
        google()
        mavenCentral()
    }

    android {
        compileSdkVersion 33
        buildToolsVersion "30.0.3"

        defaultConfig {
            minSdkVersion 24
            targetSdkVersion 33

            group = 'com.auterion.tazama'
            versionCode 1
            versionName "1.0"
        }

        buildTypes {
            release {
                minifyEnabled false
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }
        kotlinOptions {
            jvmTarget = '11'
        }

        testOptions {
            unitTests.all {
                testLogging {
                    outputs.upToDateWhen { false }
                    events "passed", "failed", "skipped", "standardError"
                    showCauses true
                    showExceptions true
                }
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

        // MAVSDK-Java
        implementation 'io.mavsdk:mavsdk:1.2.0'
        implementation 'io.mavsdk:mavsdk-server:1.2.5'

        testImplementation 'junit:junit:4.13.2'
    }
}
